/*
 * Copyright (C) 2014 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configurations {
    doclava
}

dependencies {
    doclava project(':doclava')
}

void addJavadocTasks(Project project, Task topTask, String variantName, JavaCompile javaCompile) {
    Task genJavadoc = project.tasks.findByName("generate${variantName.capitalize()}Javadoc")
    Task packJavadoc = project.tasks.findByName("package${variantName.capitalize()}Javadoc")

    if (!genJavadoc) {
        genJavadoc = project.task("generate${variantName.capitalize()}Javadoc", type: Javadoc,
                dependsOn: project.configurations.doclava) {
            title = null
            description "Generates Javadoc for $variantName."
            source = javaCompile.source
            classpath = files(javaCompile.classpath.files) + getAndroidPrebuilt(android.compileSdkVersion)
            destinationDir = file("${project.docsDir}/$variantName/javadoc")
            options.encoding = "UTF-8"
            options.doclet = "com.google.doclava.Doclava"
            options.bootClasspath = files(android.plugin.bootClasspath).asType(List)
            options.docletpath = configurations.doclava.files.asType(List)
            options.addStringOption("templatedir", "${rootDir}/../../build/tools/droiddoc/templates-sdk")
        }
    }
    genJavadoc.dependsOn topTask

    if (!packJavadoc) {
        packJavadoc = project.task("package${variantName.capitalize()}Javadoc", type: Jar) {
            classifier = "javadoc"
            from tasks["generate${variantName.capitalize()}Javadoc"].destinationDir
            destinationDir = file("${project.docsDir}/${variantName}")
        }
    }
    packJavadoc.dependsOn genJavadoc
}

// wait for all plugins to be installed
afterEvaluate {

    if (project.hasProperty("isLibrary") && project.isLibrary == false) {
        android.applicationVariants.all { variant ->

            addJavadocTasks(project, variant.assemble, variant.name, variant.javaCompile)

            task("copy${variant.name.capitalize()}Binaries", type: Copy, dependsOn: variant.assemble) {
                description "Copies the release Apk to $project.distDir"
                from variant.outputFile
                into project.distDir
                rename(variant.outputFile.name, "${project.name}-${variant.name}-${project.version}.apk")
            }
        }
    } else if (project.hasProperty("isLibrary") && project.isLibrary == true) {
        android.libraryVariants.all { variant ->

            addJavadocTasks(project, variant.assemble, variant.name, variant.javaCompile)

            task("copy${variant.name.capitalize()}Binaries", type: Copy, dependsOn: packageReleaseJar) {
                description "Copies the release Jar to $project.distDir"
                from packageReleaseJar.archivePath
                into project.distDir
                rename(packageReleaseJar.archiveName, "${project.name}-${variant.name}-${project.version}.jar")
            }
        }
    }

    task("androidSourcesJar", type: Jar, dependsOn: copyReleaseBinaries) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives androidSourcesJar
        archives packageReleaseJavadoc
    }
}
